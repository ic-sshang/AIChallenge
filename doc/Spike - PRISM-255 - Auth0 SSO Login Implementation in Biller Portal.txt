none
The 
Auth0 SSO (Single Sign-On)
 integration enables secure user authentication in the Biller Portal using the 
OAuth 2.0 Authorization Code Grant
. This integration involves both UI and backend components, JWT decoding, session setup, and secure logout.
Overview
Login Page
: 
BillerLogin.aspx
OAuth Callback Handler
: 
BillerLogin.aspx.vb
Post-login Redirect
: 
BillerWelcome.aspx
Authentication Flow
Step-by-Step
Step
Description
User clicks 
Login
 on 
BillerLogin.aspx
Redirected to Auth0's authorization endpoint
Auth0 sends back 
code
 to 
BillerLogin.aspx
code
 exchanged for access & ID tokens via 
/oauth/token
ID token decoded to extract user info
User authenticated using 
Authenticate0Biller()
Username is matched with a biller record in the database
Session initialized and redirected to 
BillerWelcome.aspx
Backend Logic
 
BillerLogin.aspx.vb
Responsibility
Redirects to Auth0 login URL
Handles OAuth callback
Exchanges code for tokens
Decodes ID token
Authenticates user and initializes session
Redirect to Auth0
Private Sub RedirectToAuth0()
    Dim auth0Domain As String = ConfigurationManager.AppSettings("Auth0Domain")
    Dim clientId As String = ConfigurationManager.AppSettings("Auth0ClientId")
    Dim callbackUrl As String = ConfigurationManager.AppSettings("Auth0CallbackUrl")
    Dim authUrl As String = $"https://{auth0Domain}/authorize?response_type=code&client_id={clientId}&redirect_uri={HttpUtility.UrlEncode(callbackUrl)}&scope=openid profile email&prompt=login"
    Response.Redirect(authUrl)
End Sub
Handle Auth0 Callback
Private Sub HandleAuth0Callback(authCode As String)
    Dim auth0Domain As String = ConfigurationManager.AppSettings("Auth0Domain")
    Dim clientId As String = ConfigurationManager.AppSettings("Auth0ClientId")
    Dim clientSecret As String = ConfigurationManager.AppSettings("Auth0ClientSecret")
    Dim callbackUrl As String = ConfigurationManager.AppSettings("Auth0CallbackUrl")
    Dim tokenUrl As String = $"https://{auth0Domain}/oauth/token"
    Dim postData As String = $"grant_type=authorization_code&client_id={clientId}&client_secret={clientSecret}&code={authCode}&redirect_uri={HttpUtility.UrlEncode(callbackUrl)}"
    
    Using client As New WebClient()
        client.Headers(HttpRequestHeader.ContentType) = "application/x-www-form-urlencoded"
        Dim response As String = client.UploadString(tokenUrl, postData)
        Dim tokenResponse As JObject = JObject.Parse(response)
        
        Dim idToken As String = tokenResponse("id_token").ToString()
        Dim userInfo As JObject = DecodeJwt(idToken)
        
        AuthenticateUser(userInfo)
    End Using
End Sub
Decode JWT
Private Function DecodeJwt(token As String) As JObject
    Dim parts As String() = token.Split("."c)
    If parts.Length < 2 Then Throw New ArgumentException("Invalid JWT token format.")
    
    Dim payload As String = parts(1).Replace("-", "+").Replace("_", "/")
    While payload.Length Mod 4 <> 0 : payload &= "=" : End While
    
    Dim decodedBytes As Byte() = Convert.FromBase64String(payload)
    Dim json As String = Encoding.UTF8.GetString(decodedBytes)
    
    Return JObject.Parse(json)
End Function
Authenticate User
Private Sub AuthenticateUser(userInfo As JObject)
    Dim username As String = userInfo("username").ToString()
    Dim a_BILLER As New Biller
    a_BILLER = a_BILLER.Authenticate0Biller(username)
    
    If a_BILLER.BillerID > 0 Then
        Session("ValidBillerSession") = True
        Session("BillerUserID") = a_BILLER.BillerAdminUserID.ToString()
        a_BILLER.BillerPermissions = a_BILLER.LoadBillerPermissions(a_BILLER.BillerAdminUserID, a_BILLER.BillerID).Tables(0)
        Session("BillerPortal_CurrentBiller") = a_BILLER
        Session("BillerPortal_CurrentBillerUser") = BillerUser.LoadBillerUser(a_BILLER.BillerAdminUserID, a_BILLER.BillerID)
        
        Response.Redirect("BillerWelcome.aspx", False)
        HttpContext.Current.ApplicationInstance.CompleteRequest()
    Else
        Me.ltrlErrorMessage.Text = "Authentication failed. Please contact support."
    End If
End Sub
Sign-out Logic (
MasterBillerPortal.Master
)
Private Sub lnkSignout_Click(sender As Object, e As EventArgs) Handles lnkSignout.Click
    Session.Abandon()
    Response.Cookies.Clear()
    Business.VisualBasic.Util.Utilities.ExpireCookie(BillerSession.CookieKey)
    
    Dim currentDomain As String = HttpContext.Current.Request.Url.GetLeftPart(UriPartial.Authority)
    Dim returnToUrl As String = HttpUtility.UrlEncode($"{currentDomain}/billerlogin.aspx?exit=1")
    Dim auth0Domain As String = ConfigurationManager.AppSettings("Auth0Domain")
    Dim logoutUrl As String = $"https://{auth0Domain}/v2/logout?client_id={ConfigurationManager.AppSettings("Auth0ClientId")}&returnTo={returnToUrl}"
    
    Response.Redirect(logoutUrl)
End Sub
Necessary Configuration Changes for Auth0 Integration
 
Web.config
 Key Entries
<appSettings>
    <add key="Auth0Domain" value="${Auth0Domain}" />
    <add key="Auth0ClientId" value="${Auth0ClientId}" />
    <add key="Auth0ClientSecret" value="${Auth0ClientSecret}" />
    <add key="Auth0CallbackUrl" value="https://localhost:62300/BillerLogin.aspx" />
</appSettings>
Callback and Logout URLs
Configure the following URLs in the Auth0 dashboard under the application settings:
Allowed Callback URLs
:
https://localhost/Auth0Callback.aspx 
Allowed Logout URLs
:
https://localhost/billerlogin.aspx 
Allowed Web Origins
:
https://localhost
Custom Claims Mapping
Ensure 
custom claims
 like 
username
 or 
role
 are configured in Auth0 Rules or Actions or Database
Limitations, Risks, and Compatibility Issues
Custom Claim Dependencies
If your app heavily relies on 
username
, 
roles
, or other custom attributes, they 
must be configured and mapped correctly
 in Auth0.
Multi-Tenant and Duplicate Email Handling
If the same email is used across different users/accounts:
Disable email verification temporarily, or
Use 
username
 or 
sub
 claim for unique identification
Recommendation
Is Auth0 Viable for Full Integration?
Auth0 is a viable and scalable solution for modernizing authentication in your Biller Portal Web Forms application â€” with the following considerations:
Benefits:
Secure OAuth 2.0 & OIDC support
Multi-Factor Authentication (MFA)
Custom claim injection (username, roles)
Federated Identity support
 (Google, Microsoft, SAML, AD)
Comprehensive user management dashboard
Caveats:
Manual token handling and validation due to Web Forms limitations
Proper setup of custom claims (
username
) is 
critical
Requires attention to error handling, logging, and fallback mechanisms
Recommendation:
Proceed with full Auth0 integration 
after ensuring proper token validation
, 
secure session management
, and 
custom claim configuration
. Auth0 is a future-proof, secure platform with strong community and enterprise support.
Future Enhancements
Customizable Login Page
: Create a custom Auth0 login page to align with the Biller Portal's branding.
Multi-Factor Authentication (MFA)
: Add MFA for additional security and enforce MFA for specific roles.
JWT Validation
: Implement robust JWT token validation with expiration, issuer, and audience checks.
Enhanced Error Handling
: Provide custom error messages for failed authentication and network issues with detailed logging.
Role-based Access Control (RBAC)
: Implement RBAC via Auth0 claims and restrict access based on user roles.
Single Logout (SLO)
: Implement Single Logout (SLO) to log users out of both the app and Auth0.
Profile Synchronization
: Sync user profile information between Auth0 and the local database.
Social Login Support
: Integrate social logins (MS, Google, Facebook) via Auth0 for more authentication options.
Audit Logs
: Enable Auth0 audit logs for tracking authentication events and user activities.
Custom Event Hooks
: Set up event hooks to trigger actions based on user activities (e.g., login, registration).
Cross-Application SSO
: Implement Cross-Domain SSO for seamless authentication across multiple applications.
Session Timeout & Auto-Logout
: Implement session timeout with expiry alerts and automatic logout on inactivity.
Account Recovery
: Provide self-service account recovery for forgotten credentials with email verification or MFA.
OIDC Connection and User Authentication Flow
Objective
: Document the process of setting up an OIDC connection in Auth0 and handling user authentication.
Steps
:
OIDC Connection Setup in Auth0 dashboard
:
Auth0 Dashboard
:
Go to 
Authentication > Enterprise
.
Click on 
Create OIDC Connection
.
Fill in the required details and click 
Create
.
Client ID and Secret
:
Replace your application's client ID and client secret with those from the OIDC connection.
Allowed Callback URL
:
Add the callback URL to the allowed callback URLs in the application settings.
Application Connections: 
Enable OIDC connection in your application connection
OIDC Document Discovery
:
Configure the OIDC document discovery URL in 
web.config
.
Call the discovery URL to retrieve metadata for OIDC-related URLs.
Fetch the token URL from the metadata.
User Authentication Flow
:
First-Time Login
:
User enters credentials.
Auth0 checks the database connection.
If the user exists, they are signed up for OIDC.
Subsequent Logins
:
User logs in via OIDC.
Auth0 authenticates the user using the OIDC connection.
Necessary Configuration Changes for OIDC Integration
Discovery URL (Web.config) 
<appSettings>
    <add key="Auth0Domain" value="${Auth0Domain}" />
    <add key="Auth0ClientId" value="${Auth0ClientId}" />
    <add key="Auth0ClientSecret" value="${Auth0ClientSecret}" />
    <add key="Auth0CallbackUrl" value="https://localhost:62300/BillerLogin.aspx" />
    <add key="DiscoveryUrl" value=".well-known/openid-configuration" />
</appSettings>
calling discovery URL to fetch OIDC metadata (BillerLogin.aspx.vb)
Dim config As JObject = GetOpenIDConfiguration()
Dim tokenUrl As String = config("token_endpoint").ToString()
Dim postData As String = $"grant_type=authorization_code&client_id={clientId}&client_secret={clientSecret}&code={authCode}&redirect_uri={HttpUtility.UrlEncode(callbackUrl)}"

Public Function GetOpenIDConfiguration() As JObject
    Using client As New WebClient()
        Dim responseString As String = client.DownloadString("https://{auth0Domain}/{DiscoveryUrl}")
        Dim config As JObject = JObject.Parse(responseString)
        Return config
    End Using
End Function
Notes
:
Ensure that the callback URL is correctly configured in both Auth0 and your application.
Handle any errors during the authentication process and provide appropriate feedback to the user.
 URL:/spaces/PE/pages/4311842842/Spike+-+PRISM-255+-+Auth0+SSO+Login+Implementation+in+Biller+Portal