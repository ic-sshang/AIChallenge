Purpose and Scope:
This domain establishes the hub-and-spoke topology for VNet isolation and private name resolutions.
Detailed Architecture
Subnet and DNS Implemetation
Terraform Implementation
hcl
# 1. Data lookups from existing hub
data "azurerm_virtual_network" "hub" {
  provider            = azurerm.hub
  name                = "vnet-${var.region}-hub${var.hub_deployment_version}${local.hub_environment_suffix}"
  resource_group_name = local.hub_resource_group_name
}

data "azurerm_subnet" "subnet_id" {
  provider            = azurerm.hub
  name                = "snet-${var.region}-hub${var.hub_deployment_version}-redis${local.hub_environment_suffix}"
  virtual_network_name = data.azurerm_virtual_network.hub.name
  resource_group_name  = data.azurerm_virtual_network.hub.resource_group_name
}

data "azurerm_private_dns_zone" "redis" {
  provider            = azurerm.hub
  name                = "privatelink.redis.cache.windows.net"
  resource_group_name = data.azurerm_virtual_network.hub.resource_group_name
}

# 2. Spoke VNet & Subnets
module "spoke_vnet" {
  source                 = "git@ssh.dev.azure.com:v3/invoicecloud/Platform/terraform-azurerm-vnet-spoke//?ref=platform-0/0.0.0"
  providers = {
    azurerm.spoke = azurerm
    azurerm.hub   = azurerm.hub
  }

  region                 = var.region
  application_name       = var.application_name
  deployment_version     = var.deployment_version
  hub_deployment_version = var.hub_deployment_version
  hub_environment        = local.hub_environment
  environment            = var.environment
  location               = module.resource_group.resource_group.location
  resource_group_name    = module.resource_group.resource_group.name
  tags                   = var.tags
}

# 3. Private DNS Zone Link
module "private_dns_zone_links" {
  source                              = "git@ssh.dev.azure.com:v3/invoicecloud/Platform/terraform-azurerm-private-dns-zone-links//?ref=platform-0/0.4.0"
  providers = { azurerm.global_resources = azurerm.hub }

  environment                         = var.environment
  region                              = var.region
  vnet_id                             = module.spoke_vnet.vnet.id
  vnet_name                           = module.spoke_vnet.vnet.name
  hub_deployment_version              = var.hub_deployment_version
  hub_environment                     = local.hub_environment
  global_resources_deployment_version = var.global_resources_deployment_version
  global_resources_environment        = local.hub_environment
  tags                                = local.tags
}

# 4. Private Endpoint for Redis
module "nonprod_in_devtest_acr_private_endpoint_per_hub" {
  for_each = azurerm_redis_cache.redis
  source   = "git@ssh.dev.azure.com:v3/invoicecloud/Platform/terraform-azurerm-private-endpoint//?ref=platform-0/0.0.1"
  providers = { azurerm = azurerm.hub }

  target_resource_name     = each.key
  target_resource_id       = each.value.id
  target_subresource_names = ["redisCache"]

  host_region                      = var.region
  host_workload_name               = var.application_name
  host_workload_deployment_version = var.deployment_version
  host_environment                 = var.environment
  host_location                    = var.location
  host_resource_group_name         = data.azurerm_virtual_network.hub.resource_group_name
  host_subnet_id                   = data.azurerm_subnet.subnet_id.id
  host_private_dns_zone_ids        = [ data.azurerm_private_dns_zone.redis.id ]

  tags = merge(var.tags, { "transition-global-infra-nonprod-to-devtest" = "true" })
}
Key Inputs and Outputs
Name
Type
Description
var.region
string
Deployment region
var.deployment_version
number
Spoke VNet version suffix
var.hub_deployment_version
number
Hub VNet version suffix
var.environment
string
Deployment environment
module.resource_group.resource_group.name
string
Resource Group for both Hub and Spoke
data.azurerm_virtual_network.hub.name
string
Name of the existing Hub VNet
data.azurerm_subnet.subnet_id.id
string
ID of the Hub Redis subnet
data.azurerm_private_dns_zone.redis.id
string
ID of the Hub’s Redis private DNS zone
var.tags
map(string)
Tag map
Output
Description
module.spoke_vnet.vnet.id
ID of the created Spoke VNet
module.spoke_vnet.vnet.name
Name of the created Spoke VNet
module.nonprod_in_devtest_acr_private_endpoint_per_hub[<key>].id
Private endpoint resource IDs
module.nonprod_in_devtest_acr_private_endpoint_per_hub[<key>].private_ip_address
Endpoint IPs for each Redis instance
Security and Access Controls:
All services use private endpoints. No Public network access.
Private DNS zones and VNet links ensure split-horizon DNS, preventing external resolution.
Hub/spoke peering is implicit within modules, with NSG rules to restrict traffic as needed.
Operations and Runbook:
Ensure 
azurerm.hub
 and 
azurerm.spoke
 providers are configured with correct aliases.  
Verify 
local.hub_environment_suffix
 matches naming convention.
#Placeholder code
terraform init  
terraform plan -var-file=env/${var.environment}.tfvars  
terraform apply -var-file=env/${var.environment}.tfvars 
Troubleshooting:
“SubnetConflict” → adjust 
var.network_address_space
 or 
cidrsubnets
 parameters.  
“Unable to resolve DNS” → confirm private DNS zone linking succeeded and VNet configurations.
Links and References
Hub VNet module: [vnet-spoke v0.0.0|
[https://dev.azure.com/invoicecloud/Platform/_git/terraform-azurerm-vnet-spoke?path=/vnet.tf]
DNS zone link module: [private-dns-zone-links v0.4.0|
https://dev.azure.com/invoicecloud/Platform/_git/terraform-azurerm-private-dns-zone-links]
 
Private endpoint module: [terraform-azurerm-private-endpoint v0.0.1|
https://dev.azure.com/invoicecloud/Platform/_git/terraform-azurerm-private-endpoint?path=/private-endpoint.tf]
 
Confluence labels: 
infra
 
networking
 
hub-spoke
 
Related pipeline: 
 URL:/spaces/platform/pages/4264427530/Networking+Hub-and-Spoke+DNS