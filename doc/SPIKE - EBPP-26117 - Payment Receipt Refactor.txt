Problem Overview
The payment confirmation pages within our application currently implement similar payment receipt displays using duplicated code with slight variations. This document outlines a plan to refactor these implementations into a standardized component that can be maintained in a single location while preserving page-specific functionality.
The confirmation pages we’ve looked at for this SPIKE are:
CartPaymentConfirmation.aspx
CloudPaymentConfirmation.aspx
Confirmation.aspx
CustomerGroupMakePaymentConfirmation.aspx
Each page implements its own version of a payment receipt display that shows total amount, payment method, and transaction details. This has led to significant code duplication, inconsistent formatting across pages, and difficulty maintaining the receipt display when changes are needed.
The current implementation suffers from several key issues:
Duplicated HTML Structure
: Nearly identical markup appears in multiple files
Repeated Business Logic
: Payment method formatting logic is duplicated and varies slightly between pages
Inconsistent Display
: The same payment types may display differently depending on which page shows the receipt
Testing Challenges
: The display logic is embedded in the pages, making it difficult to test independently
Current State Analysis
Duplicated Implementation Patterns
Each page follows a similar pattern for displaying receipt information:
Execute database queries to retrieve payment and receipt data
Calculate payment totals and service fees
Format payment method display based on payment type
Handle special cases like digital wallets
Display the formatted receipt to the user
This pattern is duplicated across pages with minor variations, resulting in unnecessary code duplication.
Differences Between Implementations
Page
Receipt Display Approach
Differences
CustomerGroupMakePaymentConfirmation.aspx
Direct in-page HTML
CartPaymentConfirmation.aspx
Direct in-page HTML
Special handling for EMV/POS terminal receipts
CloudPaymentConfirmation.aspx
Loads 
ctrlReceiptApproved.ascx
 control
Confirmation.aspx
Direct in-page HTML
Handles both EBPP and CloudStore payments with different logic
Proposed Solution
CloudPaymentConfirmation.aspx already uses a user control (
ctrlReceiptApproved.ascx
) for displaying approved payments. This control contains much of the display logic we need but is currently tied to specific data sources and not designed for reuse across all pages.
The proposed solution involves creating a standardized payment receipt framework based on the existing 
ctrlReceiptApproved.ascx
 control. This solution will separate the data retrieval, formatting logic, and display concerns while supporting all the special cases in the current implementation.
Our solution consists of three main components:
Payment Receipt Service
: A service class responsible for retrieving payment data from the database and formatting it consistently
Public Interface IPaymentReceiptService
    Function GetPaymentReceiptData(receiptGuids As List(Of String), billerID As Integer) As PaymentReceiptModel
    Function FormatPaymentMethodHtml(model As PaymentReceiptModel) As String
    Function FormatPaymentMethodNumber(model As PaymentReceiptModel) As String
End Interface

Public Class PaymentReceiptService
    Implements IPaymentReceiptService
  Public Function GetPaymentReceiptData(receiptGuids As List(Of String), billerID As Integer) As PaymentReceiptModel Implements IPaymentReceiptService.GetPaymentReceiptData
    Dim model As New PaymentReceiptModel()
    
    ' Get payment data from database
    Dim payments As DataSet = GetPaymentsData(receiptGuids, billerID)
    Dim receiptSummaries As DataSet = GetReceiptSummariesData(receiptGuids, billerID)
    
    ' Calculate totals
    For Each table As DataTable In receiptSummaries.Tables
        For Each row As DataRow In table.Rows
            model.TotalAmount += row("PaymentAmount")
            model.ServiceFee += row("ConvenienceFee")
        Next
    Next
    
    model.GrandTotalAmount = model.TotalAmount
   
    ' Set basic payment information
    If payments.Tables.Count > 0 AndAlso payments.Tables(0).Rows.Count > 0 Then
        With payments.Tables(0)(0)
            model.PaymentMessage = .Item("PaymentMessage").ToString()
            model.PaymentDate = Convert.ToDateTime(.Item("PaymentDate"))
            model.IsApproved = Convert.ToBoolean(.Item("Approved"))
            model.PaymentTypeID = Convert.ToInt32(.Item("PaymentTypeID"))
            model.PaymentTypeDescription = .Item("PaymentTypeDesc").ToString()
            model.DigitalWalletTypeID = Convert.ToInt32(.Item("DigitalWalletTypeID"))
            
            ' Set payment method details based on type
            Select Case model.PaymentTypeID
                Case PaymentType.CreditCard
                    model.CardNumber = .Item("Cardnumber").ToString()
                Case PaymentType.ACH
                    model.AccountNumber = .Item("AccountNumber").ToString()
            End Select
            
            ' Check for EMV details if present
            If Convert.ToInt32(.Item("PaymentSourceID")) = PaymentSource.Pos Then
                model.HasEmvDetails = True
                model.EmvDetails = GetEmvDetails(Convert.ToInt32(.Item("PaymentID")), billerID)
            End If
        End With
    End If
    
    ' Apply formatting
    model.FormattedPaymentMethodHtml = FormatPaymentMethodHtml(model)
    model.FormattedPaymentMethodNumber = FormatPaymentMethodNumber(model)
    
    Return model
End Function

Public Function FormatPaymentMethodHtml(model As PaymentReceiptModel) As String Implements IPaymentReceiptService.FormatPaymentMethodHtml
    Dim paymentMethodImage As String = "<img id='pmtImage' src='{0}' height='{1}' alt='{3}' />"
    Dim divEmailForPayPalBrands As String = "<br/><div>{0}</div>"
    
    ' Handle digital wallet special case
    If DigitalWalletSession.IsDigitalWallet(model.DigitalWalletTypeID) Then
        Dim digitalWallet As DigitalWalletType = model.DigitalWalletTypeID
        Dim cardType As String = GetCardTypeFromNumber(model.CardNumber)
        
        ' Custom formatting for digital wallet
        Return digitalWallet.GetDescription() & " (" & cardType & ")"
    End If
    
    ' Regular payment methods
    Select Case model.PaymentTypeID
        Case PaymentType.CreditCard
            ' Determine card type from first digit
            If String.IsNullOrEmpty(model.CardNumber) OrElse model.CardNumber.Length < 1 Then
                Return "Credit Card"
            End If
            
            Dim firstDigit As String = model.CardNumber.Substring(0, 1)
            
            Select Case firstDigit
                Case "3"
                    Return String.Format(paymentMethodImage, "../images/american_express_icon.gif", 30, "", "American Express")
                Case "4"
                    Return String.Format(paymentMethodImage, "../images/visa_icon.gif", 30, "", "Visa")
                Case "2", "5"
                    Return String.Format(paymentMethodImage, "../images/mastercard_icon.gif", 30, "", "MasterCard")
                Case "6"
                    Return String.Format(paymentMethodImage, "../images/discover_icon.gif", 30, "", "Discover")
                Case Else
                    Return "Credit Card"
            End Select
            
        Case PaymentType.ACH
            Return String.Format(paymentMethodImage, "../2/Images/Bank.png", "", "", "EFT - Check")
            
        Case PaymentType.PayPal
            Dim emailPart = String.Format(divEmailForPayPalBrands, model.EmailAddress)
            Return String.Format(paymentMethodImage, "../Images/PayPal-Logo.png", 30, emailPart, "PayPal")
            
        Case PaymentType.PayPalCredit
            Dim emailPart = String.Format(divEmailForPayPalBrands, model.EmailAddress)
            Return String.Format(paymentMethodImage, "../Images/PayPalCredit-Logo.png", 35, emailPart, "PayPal Credit")
            
        Case PaymentType.Venmo
            Dim emailPart = String.Format(divEmailForPayPalBrands, model.EmailAddress)
            Return String.Format(paymentMethodImage, "../Images/Venmo-Logo.png", 25, emailPart, "Venmo")
            
        Case Else
            Return model.PaymentTypeDescription
    End Select
End Function

Public Function FormatPaymentMethodNumber(model As PaymentReceiptModel) As String Implements IPaymentReceiptService.FormatPaymentMethodNumber
    ' Don't show number for digital wallet
    If DigitalWalletSession.IsDigitalWallet(model.DigitalWalletTypeID) Then
        Return String.Empty
    End If
    
    Select Case model.PaymentTypeID
        Case PaymentType.CreditCard
            Return clsUtilities.MaskCreditCardAccountFull(model.CardNumber)
        Case PaymentType.ACH
            Return model.AccountNumber
        Case Else
            Return String.Empty
    End Select
End Function

Private Function GetCardTypeFromNumber(cardNumber As String) As String
    If String.IsNullOrEmpty(cardNumber) OrElse cardNumber.Length < 1 Then
        Return "Credit Card"
    End If
    
    Dim firstDigit As String = cardNumber.Substring(0, 1)
    
    Select Case firstDigit
        Case "3"
            Return "American Express"
        Case "4"
            Return "Visa"
        Case "2", "5"
            Return "Mastercard"
        Case "6"
            Return "Discover"
        Case Else
            Return "Credit Card"
    End Select
End Function

Private Function GetPaymentsData(receiptGuids As List(Of String), billerID As Integer) As DataSet
    Dim sqlList As New List(Of String)
    sqlList.AddRange(receiptGuids.Select(Function(guid) "exec selPayments @BillerID=" & billerID & ", @ReceiptGuid='" & guid & "'"))
    Return clsDatabase.GET_DATASET(String.Join(";", sqlList.ToArray), billerID)
End Function

Private Function GetReceiptSummariesData(receiptGuids As List(Of String), billerID As Integer) As DataSet
    Dim sqlList As New List(Of String)
    sqlList.AddRange(receiptGuids.Select(Function(guid) "exec spReceiptSummary @BillerID=" & billerID & ", @ReceiptGuid='" & guid & "'"))
    Return clsDatabase.GET_DATASET(String.Join(";", sqlList.ToArray), billerID)
End Function

Private Function GetEmvDetails(paymentID As Integer, billerID As Integer) As EmvReceiptDetailsModel
    Dim sql As String = "exec selPaymentDetails @PaymentID=" & paymentID & _
                       ",@PaymentDetailTypeID=" & PaymentDetailType.CardReader & _
                       ",@BillerID=" & billerID
    
    Dim details As DataTable = clsDatabase.GET_DATATABLE(sql, billerID)
    
    Dim model As New EmvReceiptDetailsModel()
    
    If details.Rows.Count > 0 Then
        With details.Rows(0)
            model.EntryMethod = .Item("EntryMethod").ToString().ToTitleCase()
            
            If .Item("EntryMethod").ToString().ToUpper() <> "FALLBACK" Then
                model.ChipCardAID = .Item("ChipCardAID").ToString()
                model.ApplicationLabel = .Item("AppLab").ToString().ToLower().ToTitleCase()
            End If
            
            If details.Columns.Contains("CashierName") AndAlso Not String.IsNullOrWhiteSpace(.Item("CashierName")) Then
                model.CashierName = .Item("CashierName").ToString().ToTitleCase()
            End If
        End With
    End If
    
    Return model
End Function
End Class
Payment Receipt Model
: A data structure that holds all receipt information in a standardized format
Public Class PaymentReceiptModel
    ' Payment amounts
    Public Property TotalAmount As Double
    Public Property ServiceFee As Double
    Public Property GrandTotalAmount As Double
    
    ' Payment details
    Public Property PaymentMessage As String
    Public Property PaymentDate As DateTime
    Public Property IsApproved As Boolean
    
    ' Payment method info
    Public Property PaymentTypeID As Integer
    Public Property PaymentTypeDescription As String
    Public Property CardNumber As String
    Public Property AccountNumber As String
    Public Property DigitalWalletTypeID As Integer
    Public Property EmailAddress As String
    
    ' Display properties
    Public Property FormattedPaymentMethodHtml As String
    Public Property FormattedPaymentMethodNumber As String
    
    ' EMV stuff
    Public Property HasEmvDetails As Boolean
    Public Property EmvDetails As EmvReceiptDetailsModel
End Class

Public Class EmvReceiptDetailsModel
    Public Property EntryMethod As String
    Public Property ChipCardAID As String
    Public Property ApplicationLabel As String
    Public Property CashierName As String
    ' Additional EMV-specific properties
End Class
Enhanced Receipt Control
: An improved version of the existing ctrlReceiptApproved control that works with the model
Partial Class _2_Controls_ctrlReceiptApproved
    Inherits System.Web.UI.UserControl

  Private _receiptData As PaymentReceiptModel
  ' Single property for all receipt data
  Public Property ReceiptData As PaymentReceiptModel
      Get
          Return _receiptData
      End Get
      Set(value As PaymentReceiptModel)
          _receiptData = value
          
          ' Auto-populate values when set
          If value IsNot Nothing Then
              GrandTotalPaymentAmount = value.GrandTotalAmount
              TotalServiceFee = value.ServiceFee
              
              ' Set display fields
              If lblPaymentMessage IsNot Nothing Then lblPaymentMessage.Text = value.PaymentMessage
              If lblPaymentDate IsNot Nothing Then lblPaymentDate.Text = value.PaymentDate.ToString()
              
              ' Set payment method display
              If ltrlMVPaymentMethod IsNot Nothing Then ltrlMVPaymentMethod.Text = value.FormattedPaymentMethodHtml
              If ltrlMVPaymentMethodNumber IsNot Nothing Then ltrlMVPaymentMethodNumber.Text = value.FormattedPaymentMethodNumber
              
              ' EMV details if applicable
              SetEmvDisplay(value)
          End If
      End Set
  End Property
  
  ' Existing properties remain for backward compatibility
  Public Property GrandTotalPaymentAmount As Double
  Public Property TotalServiceFee As Double
  Public Property EmailSent As Boolean
  ' Other existing properties...
  
  Protected Sub Page_Load(ByVal sender As Object, ByVal e As System.EventArgs) Handles Me.Load
      If Not Page.IsPostBack AndAlso ReceiptData IsNot Nothing Then
          ' Update display elements
          If imgApproved IsNot Nothing AndAlso imgApprovedACH IsNot Nothing Then
              If ReceiptData.PaymentTypeID = PaymentType.ACH Then
                  imgApprovedACH.Visible = True
                  ltrlTitle.Text = "<span id=""lblConfirmationTitle"" class=""approved-title sso-approved-title"">Payment is being processed</span>"
              Else
                  imgApproved.Visible = True
                  ltrlTitle.Text = "<span id=""lblConfirmationTitle"" class=""approved-title sso-approved-title"">Thank you for your payment!</span>"
              End If
          End If
          
          ' Handle service fee display in receipt
          If TotalServiceFee > 0 Then
              pnlHasGrandTotalConvenienceFee.Visible = True
              lblHasGrandConvenienceFee.Text = String.Format("{0:c}", TotalServiceFee)
          End If
          
          lblGrandTotal.Text = String.Format("{0:c}", GrandTotalPaymentAmount)
      End If
  End Sub

  Private Sub SetEmvDisplay(data As PaymentReceiptModel)
      If data.HasEmvDetails AndAlso data.EmvDetails IsNot Nothing Then
          ' Set EMV details visibility and values
          If phShowEMVReceiptDetails IsNot Nothing Then
              phShowEMVReceiptDetails.Visible = True
              
              If data.EmvDetails.EntryMethod.ToUpper() <> "FALLBACK" AndAlso phShowEmvChipCardFields IsNot Nothing Then
                  phShowEmvChipCardFields.Visible = True
                  lblChipCardAID.Text = data.EmvDetails.ChipCardAID
                  lblApplicationLabel.Text = data.EmvDetails.ApplicationLabel
              End If
              
              If Not String.IsNullOrWhiteSpace(data.EmvDetails.CashierName) AndAlso phShowCashierName IsNot Nothing Then
                  phShowCashierName.Visible = True
                  lblCashierName.Text = data.EmvDetails.CashierName
              End If
              
              lblEntryMethod.Text = data.EmvDetails.EntryMethod
          End If
      End If
  End Sub
End Class
Sample usage
' In CartPaymentConfirmation.aspx.vb
Protected Overrides Sub OnLoad(ByVal e As System.EventArgs)
    MyBase.OnLoad(e)
  If Not Page.IsPostBack Then
      ' Create service
      Dim receiptService As New PaymentReceiptService()
      
      ' Get payment data
      Dim receiptData = receiptService.GetPaymentReceiptData(ReceiptGuids, Biller.BillerID)
      
      ' Add email (not in the database queries)
      receiptData.EmailAddress = EmailAddressUsed
      
      ' Update payment receipt display with the data
      If imgApproved IsNot Nothing AndAlso imgApprovedACH IsNot Nothing Then
          If receiptData.PaymentTypeID = PaymentType.ACH Then
              imgApprovedACH.Visible = True
              ltrlTitle.Text = "<span id=""lblConfirmationTitle"" class=""approved-title sso-approved-title"">Payment is being processed</span>"
              ltrlResultMessage.Text &= "If you provided an email address, a receipt will be emailed to you. Please allow 1-3 business days for the payment to post to your bank account, pending your bank's approval."
          Else
              imgApproved.Visible = True
              ltrlTitle.Text = "<span id=""lblConfirmationTitle"" class=""approved-title sso-approved-title"">Thank you for your payment!</span>"
              ltrlResultMessage.Text &= "A receipt was sent to your email if you gave it to us before."
          End If
      End If
      
      ' Set payment display
      ltrlMVPaymentMethod.Text = receiptData.FormattedPaymentMethodHtml
      ltrlMVPaymentMethodNumber.Text = receiptData.FormattedPaymentMethodNumber
      
      ' Continue with page-specific logic...
  End If
End Sub
Implementation Plan
User Story 1: Implement Payment Receipt Service with Data Model
As a developer, I need a centralized service with a standardized data model for retrieving and formatting payment receipt information so that all confirmation pages can use consistent data structures and business logic.
This story focuses on creating the core service that will:
Define a comprehensive data model for payment information
Implement methods to retrieve payment data from the database
Calculate payment totals and service fees consistently
Format payment method display for all payment types
Handle special cases like digital wallets and EMV receipts
The service will encapsulate all the business logic currently duplicated across pages, providing a single source of truth for payment receipt formatting.
User Story 2: Enhance Receipt Control for Reuse
As a developer, I need to enhance the ctrlReceiptApproved control to use the standardized service and model so it can be reused across all confirmation pages.
This story involves modifying the existing control to:
Work with the new payment receipt model
Properly display all payment information consistently
Handle all special cases like EMV details and digital wallets
Preserve the current visual appearance of receipts
The enhanced control will become the standard way to display payment receipts across the application.
User Story 3: Refactor CloudPaymentConfirmation and CartPaymentConfirmation Pages
As a developer, I need to update both the CloudPaymentConfirmation and CartPaymentConfirmation pages to use the new payment receipt framework while preserving their specialized functionality.
This story tackles the first two pages for integration:
CloudPaymentConfirmation already uses a control, making it a natural starting point
CartPaymentConfirmation has the most specialized functionality (EMV receipt handling)
Both pages together cover most of the special cases we need to support
Implementing these pages first will help validate our approach before moving to the others.
User Story 4: Refactor Confirmation and CustomerGroupMakePaymentConfirmation Pages
As a developer, I need to update the Confirmation and CustomerGroupMakePaymentConfirmation pages to use the new payment receipt framework while handling their specialized payment flows.
This story completes the page integration by:
Updating the remaining pages to use the new framework
Ensuring all specialized functionality is preserved
Verifying consistent receipt display across all pages
The order in which we refactor these pages is up for debate however, I’m not entirely sure what the priorities are.
 URL:/spaces/PE/pages/4131782762/SPIKE+-+EBPP-26117+-+Payment+Receipt+Refactor